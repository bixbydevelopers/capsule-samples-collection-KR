action (showImg) {
  description (선택한 모드에 맞춰 사진을 가져옵니다.)
  type(Search)
  
  collect {
    input (query) {
      type (ModeInfo)
      min (Required) max (One)
      
      // 유저 선택 값이 없을 경우 실행
      default-init {
        intent{
          goal: chooseMode
        }
      }
      
      // input 값에 대하여 검증 진행 (https://bixbydevelopers.com/dev/docs/dev-guide/developers/modeling.modeling-actions#input-validation)
      validate{
        if (query.mode.random && query.mode.images){
          
          replan{
            intent{
              goal: chooseMode
            }
          }
        }
        
        if(exists(query.mode.random) && !query.mode.random && exists(query.mode.images) && !query.mode.images){
          halt{
            dialog{
              template-macro (chooseOneMode)
            }
          }
        }
      }
    } 
  }
  output (PhotoInfo){
   // 결과에 대한 예외처리를 진행, 자바스크립트에서 던진 에러를 처리 가능
   throws{
     error (NoResult){
        on-catch {
          halt {
            dialog {
              template-macro (chooseOneMode)
            }
          }
        }
      }
      unknown-error{
        on-catch {
          halt {
            dialog {
              template-macro (unknownError)
            }
          }
        }
      }
    }
  }
}
